apply plugin: 'com.android.library'
apply plugin: 'kotlin-platform-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply plugin: 'com.github.dcendents.android-maven'
group = 'com.github.gluehome.common-android'

android {
    compileSdkVersion Versions.compile_sdk
    buildToolsVersion Versions.build_tools

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            outputs.upToDateWhen {
                false
            }
            showStandardStreams = true
        }
    }

    defaultConfig {
        minSdkVersion Versions.min_sdk
        targetSdkVersion Versions.target_sdk
    }

    buildTypes {
        debug {}
        release {}
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        main.java.srcDirs += [file("$buildDir/generated/source/kapt/main")]
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    lintOptions {
        abortOnError false
        xmlReport true
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:${Versions.kotlin}"

    // coroutines
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:${Versions.coroutines}"

    // Rx
    api 'io.reactivex.rxjava2:rxkotlin:2.3.0'

    // firebase
    api 'com.google.firebase:firebase-firestore:17.1.3'

    // logs
    api 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.sematext.android:sematext-logsene:1.0.3'

}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
